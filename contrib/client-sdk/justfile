set dotenv-load

# Default recipe to display help information
default:
  @just --list

# Generate OpenAPI spec
spec:
  cargo run -- spec

# Build generator configurations
manifest:
  mkdir -p generators
  jsonnet -S manifest.jsonnet -m generators

# Generate sdk client
codegen TARGET_LANG: manifest
  npx @openapitools/openapi-generator-cli generate \
    -i openapi.json -c generators/{{TARGET_LANG}}.yaml \
    -g {{TARGET_LANG}} -o ./{{TARGET_LANG}}

# Build all client libraries
build: spec manifest
    #!/usr/bin/env bash
    for CONFIG in "generators"/*; do
        if [ -f "$CONFIG" ]; then
            GENERATOR=$(basename $CONFIG)
            LANG=${GENERATOR%.*}
            just codegen $LANG
        fi
    done
